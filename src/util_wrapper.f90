! written by jxzou at 20200809: wrappers of utilities

module util_wrapper
 implicit none
contains

subroutine system_buf(buf)
 implicit none
 integer :: i, j
 character(len=*), intent(in) :: buf

#ifdef _WIN32
 call execute_command_line(TRIM(buf)//' > NUL', exitstat=i, cmdstat=j)
#else
 call execute_command_line(TRIM(buf)//' > /dev/null', exitstat=i, cmdstat=j)
#endif

 ! Currently there is no need to distinguish between i/=0 and j/=0
 if(.not. (i==0 .and. j==0)) then
  write(6,'(/,A)') 'ERROR in subroutine system_buf: failed to run'
  write(6,'(A)') '`'//TRIM(buf)//'`'
  stop
 end if
end subroutine system_buf

! wrapper of the utility add_bgcharge_to_inp
subroutine add_bgcharge2inp_wrap(chgname, inpname)
 implicit none
 integer :: i, j
 character(len=240), intent(in) :: chgname, inpname
 character(len=501) :: buf

 buf = 'add_bgcharge_to_inp '//TRIM(chgname)//' '//TRIM(inpname)
 call execute_command_line(TRIM(buf), exitstat=i, cmdstat=j)

 if(.not. (i==0 .and. j==0)) then
  write(6,'(/,A)') 'ERROR in subroutine add_bgcharge2inp_wrap: failed to run'
  write(6,'(A)') '`'//TRIM(buf)//'`'
  stop
 end if
end subroutine add_bgcharge2inp_wrap

! wrapper of the Gaussian utility formchk
subroutine formchk(chkname, fchname)
 implicit none
 integer :: i
 character(len=240) :: fchname0
 character(len=500) :: buf
 character(len=240), intent(in) :: chkname
 character(len=240), intent(in), optional :: fchname
 logical :: alive

 inquire(file=TRIM(chkname), exist=alive)
 if(.not. alive) then
  write(6,'(/,A)') 'ERROR in subroutine formchk: file does not exist!'
  write(6,'(A)') 'chkname='//TRIM(chkname)
  stop
 end if

 if(PRESENT(fchname)) then
  fchname0 = fchname
 else
  call find_specified_suffix(chkname, '.chk', i)
  fchname0 = chkname(1:i-1)//'.fch'
 end if

 buf = 'formchk '//TRIM(chkname)//' '//TRIM(fchname0)
 call system_buf(TRIM(buf))
end subroutine formchk

! wrapper of the Gaussian utility unfchk
subroutine unfchk(fchname, chkname)
 implicit none
 integer :: i
 character(len=240) :: chkname0
 character(len=500) :: buf
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: chkname

 if(PRESENT(chkname)) then
  chkname0 = chkname
 else
  call find_specified_suffix(fchname, '.fch', i)
  chkname0 = fchname(1:i-1)//'.chk'
 end if

 buf = 'unfchk '//TRIM(fchname)//' '//TRIM(chkname0)
 call system_buf(TRIM(buf))
end subroutine unfchk

! wrapper of the ORCA utility orca_2mkl, only .gbw -> .mkl
subroutine gbw2mkl(gbwname, mklname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: buf, mklname0
 character(len=240), intent(in) :: gbwname
 character(len=240), intent(in), optional :: mklname
 logical :: alive

 inquire(file=TRIM(gbwname), exist=alive)
 if(.not. alive) then
  write(6,'(/,A)') 'ERROR in subroutine gbw2mkl: file does not exist!'
  write(6,'(A)') 'Input gbwname='//TRIM(gbwname)
  stop
 end if

 call find_specified_suffix(gbwname, '.gbw', i)
 mklname0 = gbwname(1:i-1)//'.mkl'
 buf = 'orca_2mkl '//gbwname(1:i-1)//' -mkl'
 call system_buf(TRIM(buf))

 if(PRESENT(mklname)) then
  if(TRIM(mklname) /= TRIM(mklname0)) then
   i = RENAME(TRIM(mklname0), TRIM(mklname))
  end if
 end if
end subroutine gbw2mkl

! wrapper of orca_2json for .gbw -> .json
subroutine gbw2json(gbwname, prt_ovlp, dm_type, jsonfile)
 implicit none
 integer :: i, fid, RENAME
 integer, intent(in) :: dm_type
 character(len=240) :: old_json, json_conf, bibtex, dm_file, dm_info
 character(len=240), intent(in) :: gbwname
 character(len=240), intent(in), optional :: jsonfile
 character(len=260) :: buf
 logical, intent(in) :: prt_ovlp

 call find_specified_suffix(gbwname, '.gbw', i)
 old_json = gbwname(1:i-1)//'.json'
 json_conf = gbwname(1:i-1)//'.json.conf'
 bibtex = gbwname(1:i-1)//'.JSON.bibtex'
 dm_file = gbwname(1:i-1)//'.densities'
 dm_info = gbwname(1:i-1)//'.densitiesinfo'
 if(dm_type > 0) then
  call require_file_exist(dm_file)
  call require_file_exist(dm_info)
 end if

 open(newunit=fid,file=TRIM(json_conf),status='replace')
 write(fid,'(A)') '{'
 write(fid,'(A)') '"MOCoefficients": true,'
 write(fid,'(A)') '"Basisset": true,'
 if(prt_ovlp) write(fid,'(A)') '"1elIntegrals": ["S"],'
 select case(dm_type)
 case(0) ! no density printed
 case(1) ! unrelaxed (total) density generated by MDCI module
  write(fid,'(A)') '"Densities": ["mdcip"]'
 case(2) ! OO-MP2 density
  write(fid,'(A)') '"Densities": ["pmp2re"]'
 case(3) ! MP2 relaxed density
  write(fid,'(A)') '"Densities": ["pmp2re"]'
 case(4) ! MP2 unrelaxed density
  write(fid,'(A)') '"Densities": ["pmp2ur"]'
 case(5) ! relaxed (total) density generated by AUTOCI module
  write(fid,'(A)') '"Densities": ["autocipre"]'
 case(6) ! unrelaxed (total) density generated by AUTOCI module
  write(fid,'(A)') '"Densities": ["autocipur"]'
 case default
  write(6,'(/,A,I0)') 'ERROR in subroutine gbw2json: dm_type = ', dm_type
  write(6,'(A)') 'Currently only dm_type=0~6 are allowed.'
  close(fid)
  stop
 end select
 write(fid,'(A)') '}'
 close(fid)

 buf = 'orca_2json '//TRIM(gbwname)//' -json'
 call system_buf(TRIM(buf))
 call delete_files(2, [json_conf, bibtex])

 if(PRESENT(jsonfile)) then
  if(TRIM(jsonfile) /= TRIM(old_json)) then
   i = RENAME(TRIM(old_json), TRIM(jsonfile))
  end if
 end if
end subroutine gbw2json

! wrapper of orca_2json for .json -> .gbw
subroutine json2gbw(jsonfile, gbwname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: old_gbw, copy_gbw
 character(len=240), intent(in) :: jsonfile
 character(len=240), intent(in), optional :: gbwname
 character(len=260) :: buf

 call find_specified_suffix(jsonfile, '.json', i)
 old_gbw = jsonfile(1:i-1)//'.gbw'
 copy_gbw = jsonfile(1:i-1)//'_copy.gbw'
 buf = 'orca_2json '//TRIM(jsonfile)//' -gbw'
 call system_buf(TRIM(buf))

 if(PRESENT(gbwname)) then
  if(TRIM(gbwname) == TRIM(old_gbw)) then
   i = RENAME(TRIM(copy_gbw), TRIM(old_gbw))
  else
   i = RENAME(TRIM(copy_gbw), TRIM(gbwname))
  end if
 else
  i = RENAME(TRIM(copy_gbw), TRIM(old_gbw))
 end if
end subroutine json2gbw

! wrapper of the ORCA utility orca_2mkl, only .mkl -> .gbw
subroutine mkl2gbw(mklname, gbwname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: buf, gbwname0
 character(len=240), intent(in) :: mklname
 character(len=240), intent(in), optional :: gbwname

 call find_specified_suffix(mklname, '.mkl', i)
 gbwname0 = mklname(1:i-1)//'.gbw'
 buf = 'orca_2mkl '//mklname(1:i-1)//' -gbw'
 call system_buf(TRIM(buf))

 if(PRESENT(gbwname)) then
  if(TRIM(gbwname0) /= TRIM(gbwname)) then
   i = RENAME(TRIM(gbwname0), TRIM(gbwname))
  end if
 end if
end subroutine mkl2gbw

! wrapper of the utility fch2psi
subroutine fch2psi_wrap(fchname, inpname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: inpname0
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: inpname
 character(len=248) :: buf

 buf = 'fch2psi '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(inpname)) then
  call find_specified_suffix(fchname, '.fch', i)
  inpname0 = fchname(1:i-1)//'_psi.inp'
  if(TRIM(inpname) /= TRIM(inpname0)) then
   i = RENAME(TRIM(inpname0), TRIM(inpname))
  end if
 end if
end subroutine fch2psi_wrap

! wrapper of the utility fch2inp
subroutine fch2inp_wrap(fchname, gvb, npair, nopen, no_vec, fcgvb, prt)
 implicit none
 integer, intent(in) :: npair, nopen
 character(len=34), parameter :: error_warn='ERROR in subroutine fch2inp_wrap: '
 character(len=260) :: buf
 character(len=240), intent(in) :: fchname
 logical, intent(in) :: gvb, no_vec, fcgvb, prt

 if((.not.gvb) .and. fcgvb) then
  write(6,'(/,A)') error_warn//'fcgvb=.T. but gvb=.F., confict options.'
  write(6,'(A)') 'fchname='//TRIM(fchname)
  stop
 end if

 if(gvb .and. no_vec) then
  write(6,'(/,A)') error_warn//'confict options.'
  write(6,'(A)') 'Logical variables gvb and no_vec are both .True.'
  write(6,'(A)') 'fchname='//TRIM(fchname)
  stop
 end if

 buf = ' '

 if(gvb) then ! for GVB
  if(npair<0 .or. nopen<0) then
   write(6,'(/,A)') error_warn//'gvb=.T. but npair<0 and/or nopen<0.'
   write(6,'(A)') 'fchname='//TRIM(fchname)
   write(6,'(2(A,I0))') 'npair = ', npair, ', nopen = ', nopen
   stop
  end if
  if(nopen == 0) then
   write(buf,'(A,I0)') 'fch2inp '//TRIM(fchname)//' -gvb ', npair
  else
   write(buf,'(2(A,I0))') 'fch2inp '//TRIM(fchname)//' -gvb ',npair,' -open ',nopen
  end if
  if(fcgvb) buf = TRIM(buf)//' -fc'

 else         ! for R(O)HF, UHF, CAS
  buf = 'fch2inp '//TRIM(fchname)
  if(no_vec) buf = TRIM(buf)//' -novec'
 end if

 if(prt) write(6,'(A)') '$'//TRIM(buf)
 call system_buf(TRIM(buf))
end subroutine fch2inp_wrap

subroutine mkl2fch_wrap(mklname, fchname, ino, irel)
 implicit none
 integer, intent(in), optional :: ino, irel
 character(len=240), intent(in) :: mklname, fchname
 character(len=500) :: buf

 buf = 'mkl2fch '//TRIM(mklname)//' '//TRIM(fchname)

 if(PRESENT(ino)) then
  select case(ino)
  case(0) ! do nothing
  case(1)
   buf = TRIM(buf)//' -no'
  case(2)
   buf = TRIM(buf)//' -nso'
  case default
   write(6,'(/,A,I0)') 'ERROR in subroutine mkl2fch_wrap: invalid ino=', ino
   write(6,'(A)') 'Only {0,1,2} are allowed.'
   stop
  end select
 end if

 if(PRESENT(irel)) then
  select case(irel)
  case(-3) ! sfX2C
   buf = TRIM(buf)//' -sfx2c'
  case(-1) ! do nothing
  case(2)  ! DKH2
   buf = TRIM(buf)//' -dkh2'
  case default
   write(6,'(/,A,I0)') 'ERROR in subroutine mkl2fch_wrap: invalid irel=',irel
   write(6,'(A)') 'Only {-3,-1,2} are allowed.'
   stop
  end select
 end if

 call system_buf(TRIM(buf))
end subroutine mkl2fch_wrap

subroutine fch2mkl_wrap(fchname, mklname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: mklname0
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: mklname
 character(len=248) :: buf

 buf = 'fch2mkl '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(mklname)) then
  call find_specified_suffix(fchname, '.fch', i)
  mklname0 = fchname(1:i-1)//'_o.mkl'
  if(TRIM(mklname0) /= TRIM(mklname)) then
   i = RENAME(TRIM(mklname0), TRIM(mklname))
  end if
 end if
end subroutine fch2mkl_wrap

subroutine chk2gbw(chkname)
 implicit none
 integer :: i
 character(len=240) :: fchname, inpname, mklname, gbwname
 character(len=240), intent(in) :: chkname

 call find_specified_suffix(chkname, '.chk', i)
 fchname = chkname(1:i-1)//'.fch'
 inpname = chkname(1:i-1)//'_o.inp'
 mklname = chkname(1:i-1)//'_o.mkl'
 gbwname = chkname(1:i-1)//'.gbw'
 call formchk(chkname)

 call fch2mkl_wrap(fchname)
 open(newunit=i,file=TRIM(fchname),status='old')
 close(unit=i,status='delete')
 open(newunit=i,file=TRIM(inpname),status='old')
 close(unit=i,status='delete')

 call mkl2gbw(mklname, gbwname)
 open(newunit=i,file=TRIM(mklname),status='old')
 close(unit=i,status='delete')
end subroutine chk2gbw

! wrapper of fch_u2r
subroutine fch_u2r_wrap(fchname, new_fch)
 implicit none
 integer :: i, RENAME
 character(len=240) :: rfch
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: new_fch
 character(len=248) :: buf

 buf = 'fch_u2r '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(new_fch)) then
  call find_specified_suffix(fchname, '.fch', i)
  rfch = fchname(1:i-1)//'_r.fch'
  if(TRIM(rfch) /= TRIM(new_fch)) then
   i = RENAME(TRIM(rfch), TRIM(new_fch))
  end if
 end if
end subroutine fch_u2r_wrap

subroutine fch2dal_wrap(fchname, dalname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: molname, dalname1, molname1
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: dalname
 character(len=248) :: buf

 buf = 'fch2dal '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(dalname)) then
  call find_specified_suffix(dalname, '.dal', i)
  molname = dalname(1:i-1)//'.mol'
  call find_specified_suffix(fchname, '.fch', i)
  dalname1 = fchname(1:i-1)//'.dal'
  molname1 = fchname(1:i-1)//'.mol'
  i = RENAME(TRIM(dalname1), TRIM(dalname))
  i = RENAME(TRIM(molname1), TRIM(molname))
 end if
end subroutine fch2dal_wrap

! wrapper of utility fch2qchem
subroutine fch2qchem_wrap(fchname, job_type, npair, inpname)
 implicit none
 integer :: i, RENAME
 integer, intent(in) :: job_type, npair
 character(len=36), parameter :: error_warn='ERROR in subroutine fch2qchem_wrap: '
 character(len=240) :: inpname0, dirname
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: inpname
 character(len=260) :: buf
 character(len=480) :: scr_dir, scr_dir0

 buf = 'fch2qchem '//TRIM(fchname)
 if(npair > 0) then
  if(job_type /= 1) then
   write(6,'(/,A)') error_warn//'npair>0 is only allowed when job_type=1.'
   write(6,'(2(A,I0))') 'job_type=', job_type, ', npair=', npair
   stop
  end if
  write(buf,'(A,I0)') TRIM(buf)//' -gvb ', npair
 end if

 select case(job_type)
 case(0,1) ! nothing to append
 case(2) ! SF-CIS
  buf = TRIM(buf)//' -sfcis'
 case(3) ! SA-SF-CIS
  buf = TRIM(buf)//' -sasfcis'
 case(4) ! SF-TDDFT
  buf = TRIM(buf)//' -sf'
 case(5) ! SA-SF-DFT
  buf = TRIM(buf)//' -sasf'
 case(6) ! ADC(2)
  buf = TRIM(buf)//' -adc2'
 case(7) ! SOS-ADC(2)
  buf = TRIM(buf)//' -sosadc2'
 case default
  write(6,'(/,A)') error_warn//'job_type is out of range!'
  write(6,'(A,I0)') 'Only 0~7 are allowed. But got job_type=', job_type
  stop
 end select

 call system_buf(TRIM(buf))

 if(PRESENT(inpname)) then
  dirname = ' '
  call getenv('QCSCRATCH', dirname)

  call find_specified_suffix(fchname, '.fch', i)
  scr_dir0 = TRIM(dirname)//'/'//fchname(1:i-1)
  inpname0 = fchname(1:i-1)//'.in'
  i = RENAME(TRIM(inpname0), TRIM(inpname))

  i = INDEX(inpname, '.in', back=.true.)
  scr_dir = TRIM(dirname)//'/'//inpname(1:i-1)
  call remove_dir(TRIM(scr_dir))
  i = RENAME(TRIM(scr_dir0), TRIM(scr_dir))
 end if
end subroutine fch2qchem_wrap

subroutine bas_fch2py_wrap(fchname, dft, pyname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: pyname0
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: pyname
 character(len=256) :: buf
 logical, intent(in) :: dft

 buf = 'bas_fch2py '//TRIM(fchname)
 if(dft) buf = TRIM(buf)//' -dft'
 call system_buf(TRIM(buf))

 if(PRESENT(pyname)) then
  call find_specified_suffix(fchname, '.fch', i)
  pyname0 = fchname(1:i-1)//'.py'
  if(TRIM(pyname0) /= TRIM(pyname)) then
   i = RENAME(TRIM(pyname0), TRIM(pyname))
  end if
 end if
end subroutine bas_fch2py_wrap

subroutine fch2com_wrap(fchname, inpname)
 implicit none
 integer :: i, RENAME
 character(len=240) :: inpname1
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: inpname
 character(len=248) :: buf

 buf = 'fch2com '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(inpname)) then
  call find_specified_suffix(fchname, '.fch', i)
  inpname1 = fchname(1:i-1)//'.com'
  if(TRIM(inpname1) /= TRIM(inpname)) then
   i = RENAME(TRIM(inpname1), TRIM(inpname))
  end if
 end if
end subroutine fch2com_wrap

subroutine fch2inporb_wrap(fchname, prt_no, inpname)
 implicit none
 integer :: i, k, RENAME
 character(len=240) :: old_inp, old_orb, orbname
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: inpname
 character(len=255) :: buf
 logical, intent(in) :: prt_no

 buf = 'fch2inporb '//TRIM(fchname)
 if(prt_no) buf = TRIM(buf)//' -no'
 call system_buf(TRIM(buf))

 if(PRESENT(inpname)) then
  call find_specified_suffix(fchname, '.fch', k)
  old_inp = fchname(1:k-1)//'.input'
  if(TRIM(inpname) /= TRIM(old_inp)) then
   old_orb = fchname(1:k-1)//'.INPORB'
   call find_specified_suffix(inpname, '.input', i)
   orbname = inpname(1:i-1)//'.INPORB'
   i = RENAME(TRIM(old_inp), TRIM(inpname))
   i = RENAME(TRIM(old_orb), TRIM(orbname))
   call modify_orbname_in_molcas_inp(inpname, orbname)
  end if
 end if
end subroutine fch2inporb_wrap

subroutine orb2fch_wrap(orbname, fchname, prt_no)
 implicit none
 character(len=500) :: buf
 character(len=240), intent(in) :: orbname, fchname
 logical, intent(in) :: prt_no

 buf = 'orb2fch '//TRIM(orbname)//' '//TRIM(fchname)
 if(prt_no) buf = TRIM(buf)//' -no'
 call system_buf(TRIM(buf))
end subroutine orb2fch_wrap

! modify the orbital filename in a (Open)Molcas .input file
subroutine modify_orbname_in_molcas_inp(inpname, orbfile)
 implicit none
 integer :: i, j, fid, fid1, RENAME
 character(len=240) :: buf, inpname1
 character(len=240), intent(in) :: inpname, orbfile

 call find_specified_suffix(inpname, '.inp', i)
 inpname1 = inpname(1:i-1)//'.t'

 open(newunit=fid,file=TRIM(inpname),status='old',position='rewind')
 open(newunit=fid1,file=TRIM(inpname1),status='replace')

 do while(.true.)
  read(fid,'(A)',iostat=i) buf
  if(i /= 0) exit
  if(buf(1:7) == 'FILEORB') exit
  write(fid1,'(A)') TRIM(buf)
 end do ! for while

 if(i /= 0) then
  close(fid)
  close(fid1,status='delete')
  write(6,'(/,A)') "ERROR in subroutine modify_orbname_in_molcas_inp: keyword '&
                   &FILEORB' not found"
  write(6,'(A)') 'in file '//TRIM(inpname)
  stop
 end if

 j = INDEX(buf, '=')
 buf = buf(1:j)//' '//TRIM(orbfile)
 write(fid1,'(A)') TRIM(buf)

 do while(.true.)
  read(fid,'(A)',iostat=i) buf
  if(i /= 0) exit
  write(fid1,'(A)') TRIM(buf)
 end do ! for while

 close(fid,status='delete')
 close(fid1)
 i = RENAME(TRIM(inpname1), TRIM(inpname))
end subroutine modify_orbname_in_molcas_inp

subroutine fch2cfour_wrap(fchname)
 implicit none
 character(len=240), intent(in) :: fchname
 character(len=250) :: buf

 buf = 'fch2cfour '//TRIM(fchname)
 call system_buf(TRIM(buf))
end subroutine fch2cfour_wrap

subroutine fch2amo_wrap(fchname, aipname)
 implicit none
 integer :: i, SYSTEM, RENAME
 character(len=240) :: aipname0, amoname0, amoname
 character(len=240), intent(in) :: fchname
 character(len=240), intent(in), optional :: aipname
 character(len=248) :: buf

 call find_specified_suffix(fchname, '.', i)
 aipname0 = fchname(1:i-1)//'.aip'
 amoname0 = fchname(1:i-1)//'.amo'

 if(PRESENT(aipname)) then
  call find_specified_suffix(aipname, '.aip', i)
  amoname = aipname(1:i-1)//'.amo'
 end if

 buf = 'fch2amo '//TRIM(fchname)
 call system_buf(TRIM(buf))

 if(PRESENT(aipname)) then
  if(TRIM(aipname) /= TRIM(aipname0)) then
   i = RENAME(TRIM(aipname0), TRIM(aipname))
   i = RENAME(TRIM(amoname0), TRIM(amoname))
  end if
 end if

 i = SYSTEM('a2m '//TRIM(amoname))
 if(i /= 0) then
  write(6,'(/,A)') 'Warning from fch2amo_wrap: failed to call the utility a2m. &
                   &You need to convert'
  write(6,'(A)') '.amo to .mo by yourself, since Amesp reads MOs from the .mo f&
                 &ile.'
 end if
end subroutine fch2amo_wrap

subroutine fch2mrcc_wrap(fchname, job_type)
 implicit none
 integer, intent(in) :: job_type
 character(len=240), intent(in) :: fchname
 character(len=258) :: buf

 buf = 'fch2mrcc '//TRIM(fchname)

 select case(job_type)
 case(0) ! do nothing
 case(1)
  buf = TRIM(buf)//' -adc2'
 case(2)
  buf = TRIM(buf)//' -sosadc2'
 case(3)
  buf = TRIM(buf)//' -scsadc2'
 case(4)
  buf = TRIM(buf)//' -cc2'
 case(5)
  buf = TRIM(buf)//' -soscc2'
 case(6)
  buf = TRIM(buf)//' -scscc2'
 case default
  write(6,'(/,A)') 'ERROR in subroutine fch2mrcc_wrap: job_type is out of range!'
  write(6,'(A,I0)') 'Only 0~6 are allowed. But got job_type=', job_type
  write(6,'(A)') 'fchname='//TRIM(fchname)
  stop
 end select

 call system_buf(TRIM(buf))
end subroutine fch2mrcc_wrap

subroutine dat2fch_wrap(datname, fchname)
 implicit none
 integer :: i
 character(len=240) :: fchname0
 character(len=240), intent(in) :: datname
 character(len=240), intent(in), optional :: fchname
 character(len=500) :: buf

 if(PRESENT(fchname)) then
  fchname0 = fchname
 else
  call find_specified_suffix(datname, '.dat', i)
  fchname0 = datname(1:i-1)//'.fch'
 end if

 buf = 'dat2fch '//TRIM(datname)//' '//TRIM(fchname0)
 write(6,'(A)') '$'//TRIM(buf)
 call system_buf(TRIM(buf))
end subroutine dat2fch_wrap

subroutine fch2openqp_wrap(fchname, sf_type, inpname)
 implicit none
 integer :: i
 integer, intent(in) :: sf_type
 character(len=240) :: new_fch, old_inp
 character(len=240), intent(in) :: fchname
 character(len=240), optional :: inpname
 character(len=270) :: buf
 logical :: change_name

 call find_specified_suffix(fchname, '.fch', i)
 old_inp = fchname(1:i-1)//'.inp'

 change_name = .false.
 if(PRESENT(inpname)) then
  if(TRIM(inpname) /= TRIM(old_inp)) change_name = .true.
 end if

 if(change_name) then
  call find_specified_suffix(inpname, '.inp', i)
  new_fch = inpname(1:i-1)//'.fch'
  call sys_copy_file(TRIM(fchname), TRIM(new_fch), .false.)
 else
  new_fch = fchname
 end if

 buf = 'fch2openqp '//TRIM(new_fch)

 select case(sf_type)
 case(1) ! SF-CIS
  buf = TRIM(buf)//' -sfcis'
 case(2) ! SF-TDDFT
  buf = TRIM(buf)//' -sf'
 case(3) ! MRSF-CIS
  buf = TRIM(buf)//' -mrsfcis'
 case(4) ! MRSF-TDDFT
  buf = TRIM(buf)//' -mrsf'
 end select

 call system_buf(TRIM(buf))
 if(change_name) call delete_file(TRIM(new_fch))
end subroutine fch2openqp_wrap

! call `orca_2mkl` to convert .gbw -> .molden
subroutine gbw2molden(gbwname, molden)
 implicit none
 integer :: i
 character(len=240) :: molden0
 character(len=240), intent(in) :: gbwname
 character(len=240), intent(in), optional :: molden
 character(len=511) :: buf

 call find_specified_suffix(gbwname, '.gbw', i)
 if(present(molden)) then
  molden0 = molden
 else
  molden0 = gbwname(1:i-1)//'.molden'
 end if

 buf = 'orca_2mkl '//TRIM(gbwname)//' '//TRIM(molden0)//' -molden -anyorbs'
 call system_buf(TRIM(buf))
end subroutine gbw2molden

! wrapper for molden2fch
subroutine molden2fch_wrap(molden, fchname, prog, natorb)
 implicit none
 integer :: i, RENAME
 character(len=*), intent(in) :: prog ! lower case
 character(len=240) :: fchname0
 character(len=240), intent(in) :: molden, fchname
 character(len=500) :: buf
 logical, intent(in) :: natorb

 call find_specified_suffix(molden, '.molden', i)
 fchname0 = molden(1:i-1)//'.fch'

 buf = 'molden2fch '//TRIM(molden)//' -'//TRIM(prog)
 if(natorb) buf = TRIM(buf)//' -no'
 call system_buf(TRIM(buf))

 if(TRIM(fchname) /= TRIM(fchname0)) then
  i = RENAME(TRIM(fchname0), TRIM(fchname))
 end if
end subroutine molden2fch_wrap

subroutine fch_sph2cart_wrap(sph_fch, cart_fch)
 implicit none
 integer :: i
 character(len=240) :: def_fch
 character(len=240), intent(in) :: sph_fch
 character(len=240), optional :: cart_fch
 character(len=500) :: buf

 call find_specified_suffix(sph_fch, '.fch', i)
 def_fch = sph_fch(1:i-1)//'_c.fch'
 buf = 'fch_sph2cart '//TRIM(sph_fch)

 if(PRESENT(cart_fch)) then
  if(TRIM(def_fch) /= TRIM(cart_fch)) then
   buf = TRIM(buf)//' '//TRIM(cart_fch)
  end if
 end if

 call system_buf(TRIM(buf))
end subroutine fch_sph2cart_wrap

! wrapper for subroutine gvb_exclude_XH_A
subroutine gvb_exclude_XH_A_wrap(datname, gmsname, reverted, new_inp)
 implicit none
 integer :: i, fid, SYSTEM
 character(len=500) :: buf
 character(len=18), parameter :: txt = 'gvb_exclude_XH.txt'
 character(len=240), intent(in) :: datname, gmsname
 character(len=240), intent(out) :: new_inp
 logical, intent(in) :: reverted

 buf = 'gvb_exclude_XH '//TRIM(datname)//' '//TRIM(gmsname)
 if(reverted) buf = TRIM(buf)//' r' ! reverted use of this utility
 write(6,'(/,A)') '$'//TRIM(buf)
 i = SYSTEM(TRIM(buf)//' >'//txt//" 2>&1")

 if(i /= 0) then
  write(6,'(/,A)') 'ERROR in subroutine gvb_exclude_XH_A_wrap: failed to call&
                   & utility gvb_exclude_XH.'
  write(6,'(A)') 'Did you delete it or forget to compiled it?'
  stop
 end if

 open(newunit=fid,file=txt,status='old',position='rewind')
 do i = 1, 3
  read(fid,'(A)') buf
  write(6,'(A)') TRIM(buf)
 end do ! for i
 close(fid,status='delete')

 i = INDEX(buf, ':')
 read(buf(i+1:),*) new_inp
end subroutine gvb_exclude_XH_A_wrap

end module util_wrapper

subroutine prt_orca_2mkl_error(fname)
 implicit none
 character(len=240), intent(in) :: fname

 write(6,'(/,A)') 'ERROR: failed to call ORCA utility orca_2mkl. 3 possible rea&
                  &sons:'
 write(6,'(A)') '(1) Your ORCA environment variables are incorrect.'
 write(6,'(A)') '(2) ORCA utility orca_2mkl does not exist.'
 write(6,'(A)') '(3) The file '//TRIM(fname)//' may be incomplete.'
 stop
end subroutine prt_orca_2mkl_error

subroutine prt_call_util_error(utilname, fname)
 implicit none
 character(len=*), intent(in) :: utilname
 character(len=240), intent(in) :: fname

 write(6,'(/,A)') 'ERROR in subroutine '//utilname//'_wrap: failed to call util&
                  &ity '//utilname//'.'
 write(6,'(A)') 'fname='//TRIM(fname)
 stop
end subroutine prt_call_util_error

